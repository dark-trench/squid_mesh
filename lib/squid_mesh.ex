defmodule SquidMesh do
  @moduledoc """
  Public entrypoint for the Squid Mesh runtime.

  The API exposed here stays focused on declarative workflow operations. Host
  applications start, inspect, and later control runs through this surface
  without needing to work directly with persistence internals.
  """

  alias SquidMesh.Config
  alias SquidMesh.ReadModel.Inspection
  alias SquidMesh.ReadModel.Listing
  alias SquidMesh.Runs.GraphInspection
  alias SquidMesh.Runtime.Journal.Cancellation
  alias SquidMesh.Runtime.Journal.ChildStarter
  alias SquidMesh.Runtime.Journal.Executor
  alias SquidMesh.Runtime.Journal.Options
  alias SquidMesh.Runtime.Journal.Replay
  alias SquidMesh.Runtime.Journal.SignalInterpreter
  alias SquidMesh.Runtime.Journal.Starter
  alias SquidMesh.Runtime.Signal

  @read_models [:read_model]
  @runtimes [:journal]
  @projection_snapshot_options [:queue, :now]
  @projection_list_options [:queue, :now]
  @journal_start_options [:runtime, :journal_storage, :queue, :now, :run_id]
  @journal_child_start_options [
    :runtime,
    :journal_storage,
    :queue,
    :now,
    :child_key,
    :metadata
  ]
  @journal_control_options [:runtime, :journal_storage, :queue, :now]
  @journal_execute_options [:runtime, :journal_storage, :queue, :owner_id, :lease_for, :now]

  @typedoc """
  Structured validation errors returned by the public read-model APIs.
  """
  @type read_option_error ::
          {:invalid_option,
           {:opts, term()}
           | {:read_model, term()}
           | {:journal_storage, nil}
           | {:run_id, term()}}

  @typedoc """
  Structured validation errors returned by the public start APIs.
  """
  @type start_option_error ::
          {:invalid_option,
           {:opts, term()}
           | {:runtime, term()}
           | {:journal_storage, nil}
           | {:queue, term()}
           | {:now, term()}
           | {:run_id, term()}
           | {:schedule_idempotency_key, term()}}

  @doc """
  Loads Squid Mesh configuration from the application environment with optional
  runtime overrides.
  """
  @spec config(keyword()) :: {:ok, Config.t()} | {:error, Config.config_error()}
  defdelegate config(overrides \\ []), to: Config, as: :load

  @doc """
  Loads Squid Mesh configuration and raises if required keys are missing.
  """
  @spec config!(keyword()) :: Config.t()
  defdelegate config!(overrides \\ []), to: Config, as: :load!

  @doc """
  Starts a new workflow run through the workflow's default trigger.
  """
  @spec start(module(), map()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error()}
          | {:error, start_option_error()}
          | {:error, Starter.start_error()}
          | {:error, {:dispatch_failed, term()}}
  def start(workflow, payload) when is_map(payload), do: start(workflow, payload, [])

  @doc """
  Starts a new workflow run through the workflow's default trigger with runtime
  overrides, or through a named trigger without runtime overrides.
  """
  @spec start(module(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error()}
          | {:error, {:invalid_option, atom()}}
          | {:error, start_option_error()}
          | {:error, Starter.start_error()}
          | {:error, {:dispatch_failed, term()}}
  def start(workflow, payload, overrides) when is_map(payload) and is_list(overrides) do
    with :ok <- reject_public_start_options(overrides),
         {:ok, :journal} <- runtime(overrides) do
      start_default_run_with_runtime(:journal, workflow, payload, overrides)
    end
  end

  def start(_workflow, _payload, overrides) when is_list(overrides) do
    {:error, {:invalid_payload, :expected_map}}
  end

  @spec start(module(), atom(), map()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error()}
          | {:error, start_option_error()}
          | {:error, Starter.start_error()}
          | {:error, {:dispatch_failed, term()}}
  def start(workflow, trigger_name, payload)
      when is_atom(trigger_name) and is_map(payload),
      do: start(workflow, trigger_name, payload, [])

  @doc """
  Starts a named trigger while applying runtime configuration overrides.

  Overrides are intended for host-app test and integration boundaries. Runtime
  context injection is kept out of this public API so scheduled starts and other
  internal callers can keep their idempotency metadata isolated.

  The Jido journal runtime is the default and infers Ecto-backed journal storage
  from the configured repo. Pass `journal_storage:` only when a host, test, or
  integration boundary needs a non-default storage adapter. Journal execution
  supports normal action steps, immediate built-in `:log` steps, built-in
  `:wait` steps in transition and dependency workflows, and manual `:pause` or
  `:approval` boundaries.
  """
  @spec start(module(), atom(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error()}
          | {:error, {:invalid_option, atom()}}
          | {:error, start_option_error()}
          | {:error, Starter.start_error()}
          | {:error, {:dispatch_failed, term()}}
  def start(workflow, trigger_name, payload, overrides)
      when is_atom(trigger_name) and is_map(payload) and is_list(overrides) do
    with :ok <- reject_public_start_options(overrides),
         {:ok, :journal} <- runtime(overrides) do
      start_triggered_run_with_runtime(:journal, workflow, trigger_name, payload, overrides)
    end
  end

  @doc false
  @spec start_run_with_initial_context(module(), atom(), map(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:ok, {:duplicate_schedule_start, SquidMesh.ReadModel.Inspection.Snapshot.t()}}
          | {:error, Config.config_error()}
          | {:error, start_option_error()}
          | {:error, Starter.start_error()}
          | {:error, {:dispatch_failed, term()}}
  def start_run_with_initial_context(workflow, trigger_name, payload, initial_context, overrides)
      when is_atom(trigger_name) and is_map(payload) and is_map(initial_context) and
             is_list(overrides) do
    with :ok <- reject_public_start_options(overrides),
         {:ok, :journal} <- runtime(overrides) do
      start_initial_context_run_with_runtime(
        :journal,
        workflow,
        trigger_name,
        payload,
        initial_context,
        overrides
      )
    else
      {:error, reason} -> {:error, reason}
    end
  end

  @doc """
  Starts a child workflow run from a native step context.

  Child starts are deterministic for the parent run, parent step, child
  workflow, child trigger, and required `:child_key`. Duplicate calls with the
  same key return the existing child run and do not duplicate parent lineage.
  """
  @spec start_child_run(SquidMesh.Step.Context.t(), module(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error()}
          | {:error, {:invalid_option, atom() | term()}}
          | {:error, ChildStarter.start_error()}
  def start_child_run(parent_context, child_workflow, payload, overrides \\ [])

  def start_child_run(parent_context, child_workflow, payload, overrides)
      when is_map(payload) and is_list(overrides) do
    with :ok <- public_child_start_options(overrides),
         {:ok, :journal} <- runtime(overrides),
         {:ok, definition} <- SquidMesh.Workflow.Definition.load(child_workflow) do
      child_trigger = SquidMesh.Workflow.Definition.default_trigger(definition)

      ChildStarter.start_child_run(
        parent_context,
        child_workflow,
        child_trigger,
        payload,
        journal_child_start_options(overrides)
      )
    end
  end

  def start_child_run(_parent_context, _child_workflow, _payload, overrides)
      when is_list(overrides) do
    {:error, {:invalid_payload, :expected_map}}
  end

  def start_child_run(_parent_context, _child_workflow, _payload, _overrides) do
    {:error, {:invalid_option, {:opts, :invalid}}}
  end

  @spec start_child_run(SquidMesh.Step.Context.t(), module(), atom(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error()}
          | {:error, {:invalid_option, atom() | term()}}
          | {:error, {:invalid_trigger, :expected_atom}}
          | {:error, ChildStarter.start_error()}
  def start_child_run(parent_context, child_workflow, child_trigger, payload, overrides)
      when is_atom(child_trigger) and is_map(payload) and is_list(overrides) do
    with :ok <- public_child_start_options(overrides),
         {:ok, :journal} <- runtime(overrides) do
      ChildStarter.start_child_run(
        parent_context,
        child_workflow,
        child_trigger,
        payload,
        journal_child_start_options(overrides)
      )
    end
  end

  def start_child_run(_parent_context, _child_workflow, child_trigger, payload, overrides)
      when not is_atom(child_trigger) and is_map(payload) and is_list(overrides) do
    {:error, {:invalid_trigger, :expected_atom}}
  end

  def start_child_run(_parent_context, _child_workflow, _child_trigger, _payload, overrides)
      when is_list(overrides) do
    {:error, {:invalid_payload, :expected_map}}
  end

  def start_child_run(_parent_context, _child_workflow, _child_trigger, _payload, _overrides) do
    {:error, {:invalid_option, {:opts, :invalid}}}
  end

  @doc """
  Fetches one workflow run by id.

  The selected read model comes from host configuration unless overridden. The
  read model rebuilds a projection-backed snapshot from durable journal entries.
  """
  @spec inspect_run(String.t(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | read_option_error()
             | Config.config_error()
             | Inspection.snapshot_error()}
  def inspect_run(run_id, overrides \\ []) do
    with {:ok, :read_model} <- read_model(overrides) do
      inspect_projected_run(run_id, overrides)
    end
  end

  @doc """
  Fetches one workflow run as graph-oriented inspection data.

  The graph projection preserves `inspect_run/2` as the factual run snapshot and
  derives nodes and edges from that same durable state. The selected read model
  comes from host configuration unless overridden.
  """
  @spec inspect_run_graph(String.t(), keyword()) ::
          {:ok, GraphInspection.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | read_option_error()
             | Config.config_error()
             | Inspection.snapshot_error()}
  def inspect_run_graph(run_id, overrides \\ []) do
    with {:ok, :read_model} <- read_model(overrides),
         {:ok, inspection} <- inspect_graph_source(run_id, :read_model, overrides) do
      {:ok, graph_inspection(inspection, :read_model, overrides)}
    end
  end

  @doc """
  Explains the current runtime state of one workflow run.

  The result is structured diagnostic data for host apps, CLIs, and dashboards.
  Use `inspect_run/2` for the factual run snapshot and `explain_run/2` when an
  operator-facing surface needs the reason, evidence, and valid next actions for
  the run's current state.

  The selected read model comes from host configuration unless overridden. The
  read model derives diagnostics from durable journal projections.
  """
  @spec explain_run(String.t(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Explanation.Diagnostic.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | read_option_error()
             | Config.config_error()
             | SquidMesh.ReadModel.Explanation.explanation_error()}
  def explain_run(run_id, overrides \\ []) do
    with {:ok, :read_model} <- read_model(overrides) do
      explain_projected_run(run_id, overrides)
    end
  end

  @doc """
  Executes the next visible workflow attempt through the selected runtime.

  The default journal runtime claims one visible Jido journal-backed attempt,
  runs its declared step, and appends durable attempt completion or failure
  facts. Pass `journal_storage:` only when overriding the inferred Ecto storage
  boundary.
  """
  @spec execute_next(keyword()) :: Executor.execute_result()
  def execute_next(overrides \\ [])

  def execute_next(overrides) when is_list(overrides) do
    with :ok <- public_execute_options(overrides),
         {:ok, :journal} <- runtime(overrides) do
      Executor.execute_next(journal_execute_options(overrides))
    end
  end

  def execute_next(overrides), do: Executor.execute_next(overrides)

  defp public_execute_options(opts) do
    cond do
      not Keyword.keyword?(opts) ->
        :ok

      unsupported = Enum.find(Keyword.keys(opts), &(&1 not in public_execute_option_keys())) ->
        {:error, {:invalid_option, {:option, unsupported}}}

      true ->
        :ok
    end
  end

  defp public_execute_option_keys do
    [:runtime, :journal_storage, :queue, :owner_id, :lease_for, :now]
  end

  defp public_child_start_options(opts) do
    cond do
      not Keyword.keyword?(opts) ->
        {:error, {:invalid_option, {:opts, :invalid}}}

      unsupported = Enum.find(Keyword.keys(opts), &(&1 not in @journal_child_start_options)) ->
        {:error, {:invalid_option, {:option, unsupported}}}

      true ->
        :ok
    end
  end

  @doc """
  Lists workflow runs with optional filters.

  Journal-backed runtime calls return redacted listing summaries. Use
  `inspect_run/2` or `inspect_run_graph/2` with a run id when callers need
  detailed runtime state for one run.
  """
  @spec list_runs([Listing.list_filter()], keyword()) ::
          {:ok, [Listing.Summary.t()]}
          | {:error, Config.config_error() | Listing.list_error()}
  def list_runs(filters \\ [], overrides \\ []) do
    with {:ok, :journal} <- runtime(overrides) do
      list_runs_with_runtime(:journal, filters, overrides)
    end
  end

  @doc """
  Requests cancellation for an eligible workflow run.
  """
  @spec cancel(Ecto.UUID.t(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | Cancellation.cancel_error()}
  def cancel(run_id, overrides \\ []) do
    with {:ok, :journal} <- runtime(overrides) do
      cancel_run_with_runtime(:journal, run_id, overrides)
    end
  end

  @doc """
  Applies a Squid Mesh-native runtime command signal.

  Host applications can use this when they already normalize control requests
  into `SquidMesh.Runtime.Signal` envelopes. Public runtime functions such as
  `start/3`, `start/4`, `cancel/2`, `resume/3`, `approve/3`, `reject/3`, and
  `replay/2` use the same journal signal interpreter internally.
  """
  @spec apply_signal(Signal.t(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error, Config.config_error() | term()}
  def apply_signal(signal, overrides \\ [])

  def apply_signal(%Signal{} = signal, overrides) when is_list(overrides) do
    with {:ok, :journal} <- runtime(overrides) do
      SignalInterpreter.apply(signal, journal_control_options(overrides))
    end
  end

  def apply_signal(%Signal{}, _overrides), do: {:error, {:invalid_option, {:opts, :invalid}}}
  def apply_signal(_signal, _overrides), do: {:error, :invalid_signal}

  @doc """
  Resumes a run that is intentionally paused for manual intervention.
  """
  @spec resume(Ecto.UUID.t()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | term()}
  def resume(run_id), do: resume(run_id, %{}, [])

  @doc """
  Resumes a paused run with either configuration overrides or manual action
  attributes.
  """
  @spec resume(Ecto.UUID.t(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | term()}
  def resume(run_id, overrides) when is_list(overrides), do: resume(run_id, %{}, overrides)

  @spec resume(Ecto.UUID.t(), map()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | term()}
  def resume(run_id, attrs) when is_map(attrs), do: resume(run_id, attrs, [])

  @doc """
  Resumes a paused run with manual action attributes and configuration
  overrides.
  """
  @spec resume(Ecto.UUID.t(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | term()}
  def resume(run_id, attrs, overrides) when is_map(attrs) and is_list(overrides) do
    with {:ok, :journal} <- runtime(overrides),
         {:ok, signal} <- control_signal(:resume_run, run_id, attrs, overrides) do
      SignalInterpreter.apply(signal, journal_control_options(overrides))
    else
      {:error, {:invalid_signal, reason}} -> {:error, public_signal_error(reason)}
      {:error, _reason} = error -> error
    end
  end

  @doc """
  Approves a paused approval step and resumes the run through its success path.
  """
  @spec approve(Ecto.UUID.t(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | term()}
  def approve(run_id, attrs, overrides \\ []) when is_map(attrs) and is_list(overrides) do
    with {:ok, :journal} <- runtime(overrides),
         {:ok, signal} <- control_signal(:approve_run, run_id, attrs, overrides) do
      SignalInterpreter.apply(signal, journal_control_options(overrides))
    else
      {:error, {:invalid_signal, reason}} -> {:error, public_signal_error(reason)}
      {:error, _reason} = error -> error
    end
  end

  @doc """
  Rejects a paused approval step and resumes the run through its rejection path.
  """
  @spec reject(Ecto.UUID.t(), map(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | term()}
  def reject(run_id, attrs, overrides \\ []) when is_map(attrs) and is_list(overrides) do
    with {:ok, :journal} <- runtime(overrides),
         {:ok, signal} <- control_signal(:reject_run, run_id, attrs, overrides) do
      SignalInterpreter.apply(signal, journal_control_options(overrides))
    else
      {:error, {:invalid_signal, reason}} -> {:error, public_signal_error(reason)}
      {:error, _reason} = error -> error
    end
  end

  @doc """
  Creates a new run from a prior run and links it to the original run.

  Replays are blocked by default once the source run completed an irreversible
  or non-compensatable step. Pass `allow_irreversible: true` only after an
  operator has reviewed the side effect and accepted re-execution.
  """
  @spec replay(Ecto.UUID.t(), keyword()) ::
          {:ok, SquidMesh.ReadModel.Inspection.Snapshot.t()}
          | {:error,
             :not_found
             | :invalid_run_id
             | Config.config_error()
             | Replay.replay_error()}
          | {:error, {:dispatch_failed, term()}}
  def replay(run_id, overrides \\ []) do
    {replay_opts, config_overrides} = Keyword.split(overrides, [:allow_irreversible])

    with {:ok, :journal} <- runtime(config_overrides),
         {:ok, run} <- replay_run_with_runtime(:journal, run_id, replay_opts, config_overrides) do
      {:ok, run}
    else
      {:error, reason} = error when reason in [:not_found, :invalid_run_id] ->
        error

      {:error, {:unsafe_replay, _details} = reason} ->
        {:error, reason}

      {:error, {:invalid_run, _changeset} = reason} ->
        {:error, reason}

      {:error, {:invalid_option, _details} = reason} ->
        {:error, reason}

      {:error, {:incompatible_workflow_definition, _operation} = reason} ->
        {:error, reason}

      {:error, {:incompatible_workflow_definition, _operation, _metadata} = reason} ->
        {:error, reason}

      {:error, {:invalid_replay_source, _details} = reason} ->
        {:error, reason}

      {:error, %_struct{} = reason} ->
        {:error, {:dispatch_failed, reason}}

      {:error, reason} ->
        {:error, {:dispatch_failed, reason}}
    end
  end

  defp replay_run_with_runtime(:journal, run_id, replay_opts, config_overrides) do
    with {:ok, signal_opts} <- runtime_signal_options(config_overrides),
         {:ok, signal} <-
           Signal.replay_run(
             run_id,
             Keyword.merge(signal_opts, Keyword.take(replay_opts, [:allow_irreversible]))
           ) do
      SignalInterpreter.apply(signal, journal_control_options(config_overrides))
    else
      {:error, {:invalid_signal, reason}} -> {:error, public_signal_error(reason)}
      {:error, _reason} = error -> error
    end
  end

  defp start_default_run_with_runtime(:journal, workflow, payload, overrides) do
    start_signal_with_runtime(workflow, nil, payload, %{}, overrides)
  end

  defp start_triggered_run_with_runtime(:journal, workflow, trigger_name, payload, overrides) do
    start_signal_with_runtime(workflow, trigger_name, payload, %{}, overrides)
  end

  defp start_initial_context_run_with_runtime(
         :journal,
         workflow,
         trigger_name,
         payload,
         initial_context,
         overrides
       ) do
    start_signal_with_runtime(workflow, trigger_name, payload, initial_context, overrides)
  end

  defp start_signal_with_runtime(workflow, trigger_name, payload, initial_context, overrides) do
    with {:ok, signal_opts} <- runtime_signal_options(overrides),
         {:ok, signal_opts} <- maybe_put_schedule_idempotency(signal_opts, initial_context),
         {:ok, signal} <-
           start_signal(workflow, trigger_name, payload, initial_context, signal_opts) do
      overrides =
        overrides
        |> journal_start_options()
        |> Keyword.put(:initial_context, initial_context)

      SignalInterpreter.apply(signal, overrides)
    else
      {:error, {:invalid_signal, reason}} -> {:error, public_signal_error(reason)}
      {:error, _reason} = error -> error
    end
  end

  defp start_signal(workflow, trigger_name, payload, initial_context, signal_opts) do
    if schedule_context?(initial_context) do
      Signal.start_cron(workflow, trigger_name, payload, signal_opts)
    else
      Signal.start_run(workflow, trigger_name, payload, signal_opts)
    end
  end

  defp schedule_context?(context) when is_map(context) do
    case schedule_context(context) do
      schedule when is_map(schedule) -> map_size(schedule) > 0
      _other -> false
    end
  end

  defp maybe_put_schedule_idempotency(signal_opts, context) when is_map(context) do
    context
    |> schedule_context()
    |> schedule_receipt_idempotency_key()
    |> case do
      {:ok, nil} -> {:ok, signal_opts}
      {:ok, idempotency_key} -> {:ok, Keyword.put(signal_opts, :idempotency_key, idempotency_key)}
      {:error, _reason} = error -> error
    end
  end

  defp schedule_receipt_idempotency_key(schedule) when is_map(schedule) do
    case schedule_value(schedule, :idempotency_key) do
      idempotency_key when is_binary(idempotency_key) ->
        {:ok, idempotency_key}

      nil ->
        case schedule_value(schedule, :signal_id) do
          signal_id when is_binary(signal_id) -> {:ok, signal_id}
          nil -> {:ok, nil}
          _invalid -> {:error, {:invalid_option, {:schedule_idempotency_key, :invalid}}}
        end

      _invalid ->
        {:error, {:invalid_option, {:schedule_idempotency_key, :invalid}}}
    end
  end

  defp schedule_receipt_idempotency_key(_schedule), do: {:ok, nil}

  defp runtime_signal_options(overrides) do
    case Keyword.fetch(overrides, :now) do
      {:ok, %DateTime{} = now} -> {:ok, [occurred_at: now]}
      {:ok, _invalid} -> {:error, {:invalid_option, {:now, :invalid}}}
      :error -> {:ok, []}
    end
  end

  defp journal_child_start_options(overrides) do
    configured_journal_options(overrides, @journal_child_start_options)
  end

  defp list_runs_with_runtime(:journal, filters, overrides) do
    with {:ok, storage} <- journal_storage(overrides) do
      Listing.list(storage, filters, journal_list_options(overrides))
    end
  end

  defp cancel_run_with_runtime(:journal, run_id, overrides) do
    case control_signal(:cancel_run, run_id, overrides) do
      {:ok, signal} ->
        SignalInterpreter.apply(signal, journal_control_options(overrides))

      {:error, {:invalid_signal, reason}} ->
        {:error, public_signal_error(reason)}

      {:error, _reason} = error ->
        error
    end
  end

  defp control_signal(:cancel_run, run_id, overrides) do
    with {:ok, signal_opts} <- control_signal_options(overrides) do
      run_id
      |> Signal.cancel_run(signal_opts)
      |> normalize_control_signal_result()
    end
  end

  defp control_signal(type, run_id, attrs, overrides) do
    with {:ok, signal_opts} <- control_signal_options(overrides) do
      Signal
      |> apply(type, [run_id, attrs, signal_opts])
      |> normalize_control_signal_result()
    end
  end

  defp control_signal_options(overrides) do
    case Keyword.fetch(overrides, :now) do
      {:ok, %DateTime{} = now} -> {:ok, [occurred_at: now]}
      {:ok, _invalid} -> {:error, {:invalid_option, {:now, :invalid}}}
      :error -> {:ok, []}
    end
  end

  defp normalize_control_signal_result({:ok, %Signal{}} = result), do: result

  defp normalize_control_signal_result({:error, {:invalid_signal, _reason}} = error), do: error

  defp normalize_control_signal_result({:error, reason}), do: {:error, {:invalid_signal, reason}}

  defp public_signal_error({:run_id, :invalid}), do: :invalid_run_id

  defp public_signal_error({:occurred_at, :expected_datetime}),
    do: {:invalid_option, {:now, :invalid}}

  defp public_signal_error(reason), do: {:invalid_signal, reason}

  defp schedule_context(context) do
    case Map.fetch(context, :schedule) do
      {:ok, schedule} -> schedule
      :error -> Map.get(context, "schedule", %{})
    end
  end

  defp schedule_value(schedule, key) when is_map(schedule) do
    case Map.fetch(schedule, key) do
      {:ok, value} -> value
      :error -> Map.get(schedule, Atom.to_string(key))
    end
  end

  defp reject_public_start_options(overrides) do
    cond do
      Keyword.has_key?(overrides, :context) ->
        {:error, {:invalid_option, :context}}

      Keyword.has_key?(overrides, :initial_context) ->
        {:error, {:invalid_option, :initial_context}}

      true ->
        :ok
    end
  end

  defp inspect_projected_run(run_id, overrides) when is_binary(run_id) do
    with {:ok, storage} <- journal_storage(overrides) do
      Inspection.snapshot(storage, run_id, projected_snapshot_options(overrides))
    end
  end

  defp inspect_projected_run(_run_id, _overrides) do
    {:error, {:invalid_option, {:run_id, :invalid}}}
  end

  defp inspect_graph_source(run_id, :read_model, overrides) do
    inspect_projected_run(run_id, overrides)
  end

  defp graph_inspection(%Inspection.Snapshot{} = snapshot, read_model, overrides) do
    GraphInspection.from_snapshot(snapshot, graph_inspection_options(read_model, overrides))
  end

  defp graph_inspection_options(read_model, overrides) do
    [
      source: read_model,
      include_details: Keyword.get(overrides, :include_history, false)
    ]
  end

  defp explain_projected_run(run_id, overrides) do
    with {:ok, storage} <- journal_storage(overrides) do
      SquidMesh.ReadModel.Explanation.explain(
        storage,
        run_id,
        projected_snapshot_options(overrides)
      )
    end
  end

  defp read_model(overrides) when is_list(overrides) do
    with :ok <- validate_keyword_options(overrides) do
      configured_read_model(overrides)
    end
  end

  defp read_model(_overrides), do: {:error, {:invalid_option, {:opts, :invalid}}}

  defp runtime(overrides) when is_list(overrides) do
    with :ok <- validate_keyword_options(overrides) do
      configured_runtime(overrides)
    end
  end

  defp runtime(_overrides), do: {:error, {:invalid_option, {:opts, :invalid}}}

  defp validate_keyword_options(overrides) do
    if Keyword.keyword?(overrides) do
      :ok
    else
      {:error, {:invalid_option, {:opts, :invalid}}}
    end
  end

  defp configured_read_model(overrides) do
    case Keyword.fetch(overrides, :read_model) do
      {:ok, read_model} when read_model in @read_models ->
        {:ok, read_model}

      {:ok, _read_model} ->
        {:error, {:invalid_option, {:read_model, :invalid}}}

      :error ->
        load_configured_read_model(overrides)
    end
  end

  defp load_configured_read_model(overrides) do
    case Config.load(config_routing_overrides(overrides)) do
      {:ok, %Config{read_model: read_model}} -> {:ok, read_model}
      {:error, _reason} = error -> error
    end
  end

  defp configured_runtime(overrides) do
    case Keyword.fetch(overrides, :runtime) do
      {:ok, runtime} when runtime in @runtimes ->
        {:ok, runtime}

      {:ok, _runtime} ->
        {:error, {:invalid_option, {:runtime, :invalid}}}

      :error ->
        load_configured_runtime(overrides)
    end
  end

  defp load_configured_runtime(overrides) do
    case Config.load(config_routing_overrides(overrides)) do
      {:ok, %Config{runtime: runtime}} -> {:ok, runtime}
      {:error, _reason} = error -> error
    end
  end

  defp journal_storage(overrides) do
    case Keyword.fetch(overrides, :journal_storage) do
      {:ok, storage} ->
        Options.storage(storage)

      :error ->
        case Config.load(config_routing_overrides(overrides)) do
          {:ok, %Config{} = config} -> Options.storage(config.journal_storage)
          {:error, {:missing_config, [:journal_storage]}} -> Options.storage(nil)
          {:error, _reason} = error -> error
        end
    end
  end

  defp projected_snapshot_options(overrides) do
    configured_journal_options(overrides, @projection_snapshot_options)
  end

  defp journal_start_options(overrides) do
    configured_journal_options(overrides, @journal_start_options)
  end

  defp journal_control_options(overrides) do
    configured_journal_options(overrides, @journal_control_options)
  end

  defp journal_execute_options(overrides) do
    configured_journal_options(overrides, @journal_execute_options)
  end

  defp journal_list_options(overrides) do
    configured_journal_options(overrides, @projection_list_options)
  end

  defp configured_journal_options(overrides, keys) do
    case load_config_for_journal_options(overrides) do
      {:ok, %Config{} = config} ->
        [
          runtime: :journal,
          journal_storage: config.journal_storage,
          queue: config.queue
        ]
        |> Keyword.merge(Keyword.take(overrides, keys))
        |> Keyword.take(keys)

      {:error, _reason} ->
        Keyword.take(overrides, keys)
    end
  end

  defp load_config_for_journal_options(overrides) do
    case Config.load(config_routing_overrides(overrides)) do
      {:ok, %Config{} = config} ->
        {:ok, config}

      {:error, _reason} ->
        overrides
        |> Keyword.delete(:journal_storage)
        |> config_routing_overrides()
        |> Config.load()
    end
  end

  defp config_routing_overrides(overrides) do
    Keyword.take(overrides, [
      :repo,
      :runtime,
      :read_model,
      :journal_storage
    ])
  end
end
